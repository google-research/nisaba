# Copyright 2024 Nisaba Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load(
    "//nisaba/scripts/utils:grammars.bzl",
    "nisaba_compile_multi_grm_py",
    "nisaba_grm_textproto_test",
)

package(
    default_applicable_licenses = [
    ],
    default_visibility = [
        "//nisaba/scripts/natural_translit/languages:__subpackages__",
    ],
)

licenses(["notice"])

LANGUAGES = [
    "hi",
    "kn",
    "ml",
    "ta",
    "te",
]

# Compile grms. Eg: //nisaba/scripts/natural_translit/g2p:hi_iso_ipa

[
    nisaba_compile_multi_grm_py(
        name = "%s_iso_ipa" % language,
        outs = {
            "byte": "%s_iso_ipa.far" % language,
            "utf8": "%s_iso_ipa_utf8.far" % language,
        },
        visibility = ["//visibility:public"],
        deps = [
            "@org_opengrm_pynini//pynini",
            "//nisaba/scripts/natural_translit/language_params:%s" % language,
        ],
    )
    for language in LANGUAGES
]

# Ordinary byte tests.
[
    nisaba_grm_textproto_test(
        name = "%s_iso_ipa_test" % language,
    )
    for language in LANGUAGES
]

# UTF-8 tests.
[
    nisaba_grm_textproto_test(
        name = "%s_iso_ipa_utf8_test" % language,
        textproto = "testdata/%s_iso_ipa.textproto" % language,
        token_type = "utf8",
    )
    for language in LANGUAGES
]
