# Copyright 2024 Nisaba Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Deromanization grms.

load(
    "//nisaba/scripts/utils:grammars.bzl",
    "nisaba_compile_multi_grm_py",
    "nisaba_grm_textproto_test",
)

package(
    default_applicable_licenses = [
    ],
    default_visibility = [
        "//nisaba/scripts/natural_translit:__subpackages__",
    ],
)

licenses(["notice"])

LANGUAGES = [
    ("hi", "deva"),
    ("ta", "taml"),
]

SPELLOUT_LANGUAGES = [
    "bn_beng",
    "gu_gujr",
    "hi_deva",
    "kn_knda",
    "ml_mlym",
    "mr_deva",
    "or_orya",
    "pa_guru",
    "sd_arab",
    "si_sinh",
    "ta_taml",
    "te_telu",
    "ur_arab",
]

# Compile ISO grms. Eg: //nisaba/scripts/natural_translit/deromanization:hi_iso

[
    nisaba_compile_multi_grm_py(
        name = "%s_iso" % language,
        outs = {
            "byte": "%s_iso.far" % language,
            "utf8": "%s_iso_utf8.far" % language,
        },
        visibility = ["//visibility:public"],
        deps = [
            "@org_opengrm_pynini//pynini",
            "//nisaba/scripts/natural_translit/language_params:%s" % language,
        ],
    )
    for language, _ in LANGUAGES
]

# Compile Brahmic grms. Eg: //nisaba/scripts/natural_translit/deromanization:hi_deva

[
    nisaba_compile_multi_grm_py(
        name = "%s_%s" % (language, script),
        outs = {
            "byte": "%s_%s.far" % (language, script),
            "utf8": "%s_%s_utf8.far" % (language, script),
        },
        visibility = ["//visibility:public"],
        deps = [
            "@org_opengrm_pynini//pynini",
            "//nisaba/scripts/natural_translit/language_params:%s" % language,
        ],
    )
    for language, script in LANGUAGES
]

[
    nisaba_compile_multi_grm_py(
        name = "en_spellout_%s" % language,
        outs = {
            "byte": "en_spellout_%s.far" % language,
            "utf8": "en_spellout_%s_utf8.far" % language,
        },
        visibility = ["//visibility:public"],
        deps = [
            "//nisaba/scripts/natural_translit/brahmic:en_spellout",
            "@org_opengrm_pynini//pynini",
        ],
    )
    for language in SPELLOUT_LANGUAGES
]

# Byte tests. Eg: //nisaba/scripts/natural_translit/deromanization:hi_iso_test

[
    nisaba_grm_textproto_test(
        name = "%s_iso_test" % language,
    )
    for language, _ in LANGUAGES
]

[
    nisaba_grm_textproto_test(
        name = "%s_%s_test" % (language, script),
    )
    for language, script in LANGUAGES
]

[
    nisaba_grm_textproto_test(
        name = "en_spellout_%s_test" % language,
    )
    for language in SPELLOUT_LANGUAGES
]

# UTF-8 tests. Eg: //nisaba/scripts/natural_translit/deromanization:hi_iso_utf8_test
[
    nisaba_grm_textproto_test(
        name = "%s_iso_utf8_test" % language,
        textproto = "testdata/%s_iso.textproto" % language,
        token_type = "utf8",
    )
    for language, _ in LANGUAGES
]

[
    nisaba_grm_textproto_test(
        name = "%s_%s_utf8_test" % (language, script),
        textproto = "testdata/%s_%s.textproto" % (language, script),
        token_type = "utf8",
    )
    for language, script in LANGUAGES
]

[
    nisaba_grm_textproto_test(
        name = "en_spellout_%s_utf8_test" % language,
        textproto = "testdata/en_spellout_%s.textproto" % language,
        token_type = "utf8",
    )
    for language in SPELLOUT_LANGUAGES
]
