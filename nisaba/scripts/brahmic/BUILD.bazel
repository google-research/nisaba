# Copyright 2021 Nisaba Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Build packages for the Brahmic script APIs.

load("@nisaba_deps//:requirements.bzl", "requirement")

load(
    "//nisaba/scripts/utils:grammars.bzl",
    "nisaba_compile_grm_py",
    "nisaba_compile_multi_grm_py",
    "nisaba_grm_regression_test",
)
load(
    "//nisaba/scripts/brahmic:constant.bzl",
    "FIXED_RULE_SCRIPTS",
    "LANG_DIRS",
    "SCRIPTS",
)

package(default_visibility = ["//nisaba/scripts/brahmic:__subpackages__"])

licenses(["notice"])

nisaba_compile_grm_py(
    name = "iso",
    data = [
        "//nisaba/scripts/brahmic/data/script/{}:{}.tsv".format(script, file_name)
        for script in SCRIPTS
        for file_name in [
            "coda",
            "consonant",
            "standalone",
            "virama",
            "vowel",
            "vowel_sign",
        ]
    ] + ["//nisaba/scripts/brahmic/data/common:symbol.tsv"],
    visibility = ["//visibility:public"],
    deps = [
        ":util",
        "//nisaba/scripts/utils:file",
        "//nisaba/scripts/utils:rewrite",
        "@org_opengrm_pynini//pynini",
        "@org_opengrm_pynini//pynini/lib:pynutil",
    ],
)

nisaba_grm_regression_test(
    name = "iso_test",
    grammar = ":iso",
)

nisaba_compile_multi_grm_py(
    name = "fixed",
    outs = {
        "byte": "fixed.far",
        "utf8": "fixed_utf8.far",
    },
    data = [":sigma_utf8.far"] + [
        "//nisaba/scripts/brahmic/data/script/{}:fixed.tsv".format(script)
        for script in FIXED_RULE_SCRIPTS
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":util",
        "//nisaba/scripts/utils:char",
        "//nisaba/scripts/utils:file",
        "//nisaba/scripts/utils:rule",
        "@org_opengrm_pynini//pynini",
    ],
)

nisaba_grm_regression_test(
    name = "fixed_test",
    grammar = ":fixed",
    token_type = "byte",
)

nisaba_compile_multi_grm_py(
    name = "nfc",
    outs = {
        "byte": "nfc.far",
        "utf8": "nfc_utf8.far",
    },
    data = [
        ":sigma_utf8.far",
    ] + ["//nisaba/scripts/brahmic/data/script/{}:nfc.tsv".format(script) for script in SCRIPTS],
    visibility = ["//visibility:public"],
    deps = [
        ":util",
        "//nisaba/scripts/utils:rule",
        "@org_opengrm_pynini//pynini",
    ],
)

nisaba_grm_regression_test(
    name = "nfc_test",
    grammar = ":nfc",
    token_type = "byte",
)

nisaba_grm_regression_test(
    name = "nfc_utf8_test",
    far_file_target = ":nfc_utf8.far",
    grammar = ":nfc",
    test_file = "testdata:nfc.tsv",
    token_type = "utf8",
)

py_library(
    name = "util",
    srcs = ["util.py"],
    srcs_version = "PY3",
    deps = [
        "//nisaba/scripts/utils:file",
        "@org_opengrm_pynini//pynini",
        "@org_opengrm_pynini//pynini/lib:byte",
        "@rules_python//python/runfiles",
        requirement("pandas"),
    ],
)

py_test(
    name = "util_test",
    size = "small",
    srcs = ["util_test.py"],
    data = ["//nisaba/scripts/brahmic/data/script/Beng:dead_consonant.tsv"],
    python_version = "PY3",
    srcs_version = "PY3",
    deps = [
        ":util",
        "//nisaba/scripts/utils:file",
        "@io_abseil_py//absl/testing:absltest",
    ],
)

py_library(
    name = "char_util",
    srcs = ["char_util.py"],
    data = [
        "//nisaba/scripts/brahmic/data/script/{}:{}.tsv".format(script, file_name)
        for script in SCRIPTS
        for file_name in [
            "accept",
            "coda",
            "consonant",
            "nfc",
            "standalone",
            "virama",
            "visual_rewrite",
            "vowel",
            "vowel_sign",
        ]
    ],
    srcs_version = "PY3",
    deps = [
        ":util",
        "//nisaba/scripts/utils:char",
        "//nisaba/scripts/utils:rewrite",
        "@org_opengrm_pynini//pynini",
        "@org_opengrm_pynini//pynini/lib:pynutil",
    ],
)

nisaba_compile_grm_py(
    name = "sigma",
    out = "sigma_utf8.far",
    deps = [
        ":char_util",
        ":util",
        "//nisaba/scripts/utils:char",
        "@org_opengrm_pynini//pynini",
    ],
)

py_test(
    name = "fst_properties_test",
    srcs = ["fst_properties_test.py"],
    data = [
        ":fixed.far",
        ":fixed_utf8.far",
        ":nfc.far",
        ":nfc_utf8.far",
        ":sigma_utf8.far",
        ":visual_norm.far",
        ":visual_norm_utf8.far",
        ":wellformed.far",
        ":wellformed_utf8.far",
    ],
    python_version = "PY3",
    srcs_version = "PY3",
    deps = [
        ":util",
        "//nisaba/scripts/utils:test_util",
        "@io_abseil_py//absl/testing:absltest",
        "@io_abseil_py//absl/testing:parameterized",
        "@org_opengrm_pynini//pynini",
    ],
)

py_test(
    name = "fst_randgen_test",
    size = "large",
    srcs = ["fst_randgen_test.py"],
    data = [
        ":fixed.far",
        ":fixed_utf8.far",
        ":iso.far",
        ":nfc.far",
        ":nfc_utf8.far",
        ":visual_norm.far",
        ":visual_norm_utf8.far",
    ],
    python_version = "PY3",
    srcs_version = "PY3",
    deps = [
        ":util",
        "//nisaba/scripts/utils:file",
        "//nisaba/scripts/utils:test_util",
        "@io_abseil_py//absl/testing:absltest",
        "@io_abseil_py//absl/testing:parameterized",
        "@org_opengrm_pynini//pynini",
    ],
)

nisaba_compile_multi_grm_py(
    name = "visual_norm",
    outs = {
        "byte": "visual_norm.far",
        "utf8": "visual_norm_utf8.far",
    },
    data = [
        ":nfc.far",
        ":nfc_utf8.far",
        ":sigma_utf8.far",
    ] + [
        "//nisaba/scripts/brahmic/data/script/{}:{}.tsv".format(script, file_name)
        for script in SCRIPTS
        for file_name in [
            "consonant",
            "preserve",
            "visual_rewrite",
        ]
    ] + [
        "//nisaba/scripts/brahmic/data/lang/{}:{}.tsv".format(lang, file_name)
        for lang in LANG_DIRS
        for file_name in [
            "after_consonant",
            "before_consonant",
        ]
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":char_util",
        ":util",
        "//nisaba/scripts/utils:char",
        "//nisaba/scripts/utils:file",
        "//nisaba/scripts/utils:rewrite",
        "//nisaba/scripts/utils:rule",
        "@org_opengrm_pynini//pynini",
        "@org_opengrm_pynini//pynini/lib:pynutil",
    ],
)

nisaba_grm_regression_test(
    name = "visual_norm_test",
    grammar = ":visual_norm",
    token_type = "byte",
)

nisaba_grm_regression_test(
    name = "visual_norm_utf8_test",
    far_file_target = ":visual_norm_utf8.far",
    grammar = ":visual_norm",
    test_file = "testdata:visual_norm.tsv",
    token_type = "utf8",
)

nisaba_compile_multi_grm_py(
    name = "wellformed",
    outs = {
        "byte": "wellformed.far",
        "utf8": "wellformed_utf8.far",
    },
    data = [
        "//nisaba/scripts/brahmic/data/script/{}:{}.tsv".format(script, file_name)
        for script in SCRIPTS
        for file_name in [
            "accept",
            "coda",
            "consonant",
            "preserve",
            "standalone",
            "virama",
            "vowel",
            "vowel_sign",
        ]
    ] + [
        "//nisaba/scripts/brahmic/data/common:symbol.tsv",
        "//nisaba/scripts/brahmic/data/script/Beng:dead_consonant.tsv",
        "//nisaba/scripts/brahmic/data/script/Mlym:dead_consonant.tsv",
        "//nisaba/scripts/brahmic/data/script/Mtei:dead_consonant.tsv",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":util",
        "//nisaba/scripts/utils:file",
        "@org_opengrm_pynini//pynini",
    ],
)

nisaba_grm_regression_test(
    name = "wellformed_test",
    grammar = ":wellformed",
    token_type = "byte",
)

nisaba_grm_regression_test(
    name = "wellformed_utf8_test",
    far_file_target = ":wellformed_utf8.far",
    grammar = ":wellformed",
    test_file = "testdata:wellformed.tsv",
    token_type = "utf8",
)

py_library(
    name = "py",
    srcs = ["__init__.py"],
    data = [
        ":iso.far",
        ":nfc.far",
        ":visual_norm.far",
        ":wellformed.far",
    ],
    srcs_version = "PY3",
    visibility = ["//visibility:public"],
    deps = [
        ":util",
        "//nisaba/scripts/utils:far",
        "@org_opengrm_pynini//pynini",
    ],
)

py_test(
    name = "py_test",
    size = "small",
    srcs = ["brahmic_test.py"],
    main = "brahmic_test.py",
    python_version = "PY3",
    srcs_version = "PY3",
    deps = [
        ":py",
        "@io_abseil_py//absl/testing:absltest",
    ],
)

cc_library(
    name = "far_cc",
    srcs = ["far.cc"],
    hdrs = ["far.h"],
    data = [
        ":iso.far",
        ":nfc.far",
        ":nfc_utf8.far",
        ":visual_norm.far",
        ":visual_norm_utf8.far",
        ":wellformed.far",
        ":wellformed_utf8.far",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "//nisaba/port:file_util",
        "@com_google_absl//absl/memory",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/strings",
        "@org_openfst//:fst",
        "@org_opengrm_thrax//:thrax",
    ],
)

cc_test(
    name = "far_cc_test",
    size = "small",
    srcs = ["far_test.cc"],
    deps = [
        ":far_cc",
        "//nisaba/port:status-matchers",
        "@com_google_googletest//:gtest_main",
        "@org_openfst//:fst",
    ],
)

cc_library(
    name = "cc",
    srcs = ["grammar.cc"],
    hdrs = ["grammar.h"],
    data = [":far_files"],
    visibility = ["//visibility:public"],
    deps = [
        "//nisaba/port:file_util",
        "//nisaba/port:integral_types",
        "@com_google_absl//absl/memory",
        "@com_google_absl//absl/strings",
        "@com_google_protobuf//:protobuf",
        "@org_opengrm_thrax//:thrax",
    ],
)

cc_test(
    name = "cc_test",
    size = "small",
    srcs = ["grammar_test.cc"],
    deps = [
        ":cc",
        "//nisaba/port:status-matchers",
        "@com_google_absl//absl/memory",
        "@com_google_googletest//:gtest_main",
    ],
)

filegroup(
   name = "far_files",
   srcs = [
       ":iso.far",
       ":nfc.far",
       ":visual_norm.far",
       ":wellformed.far",
   ],
)
